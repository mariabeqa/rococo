plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'org.rococo'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //GRPC
    implementation project(':rococo-grpc-common')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "net.devh:grpc-server-spring-boot-starter:${project.ext.springGrpcVersion}"
    implementation "net.devh:grpc-client-spring-boot-starter:${project.ext.springGrpcVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'io.grpc:grpc-testing:1.64.0'
    //DB
    runtimeOnly "mysql:mysql-connector-java:${project.ext.mysqlDriverVersion}"

    annotationProcessor "org.projectlombok:lombok:${project.ext.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${project.ext.lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jib {
    container {
        ports = ['8810']
        jvmFlags = ["-Dspring.profiles.active=${System.getenv("PROFILE")}"]
        creationTime = 'USE_CURRENT_TIMESTAMP'
        labels = [
                'version'   : "${project.version}".toString()
        ]
    }
    from {
        image = "${project.ext.dockerImage}"
        platforms {
            platform {
                architecture = "${project.ext.dockerArch}"
                os = 'linux'
            }
        }
    }
    to {
        image = "${project.ext.dockerHubName}/${project.name}-${System.getenv("PROFILE")}"
        tags = ['latest', "${project.version}"]
    }
}

tasks.jib.dependsOn test
tasks.jibDockerBuild.dependsOn test


tasks.named('test') {
    useJUnitPlatform()
}
